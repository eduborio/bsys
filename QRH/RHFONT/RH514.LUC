////////////////////////////////////////////////////////////////////////////
// SISTEMA....: FOLHA DE PAGAMENTO
// OBJETIVO...: EMISSAO DA GUIA DE INSS (GRPS)
// ANALISTA...: CARLOS EDUARDO RABELLO NUNES
// PROGRAMADOR: O MESMO
// INICIO.....: JANEIRO DE 1994
// OBS........:
// ALTERACOES.:

#include "rh.ch"
#include "inkey.ch"

// DECLARACAO E INICIALIZACAO DE VARIAVEIS __________________________________

local   nCONT

private sBLOCO1   := qlbloc("B514B","QBLOC.GLO") // bloco ambiente

private lPAUSA    := .T.
private cLOCALIMP := ""
private cMENSA1
private cMENSA2
private nAUTO
private cAMBIENTE := "M"
private cANOMES
private cMESANO   := right(XANOMES,2) + left(XANOMES,4)

qmensa("<Pressione ESC para Cancelar>")

// LOOP INICIAL PARA PERMANECER DENTRO DA OPCAO _____________________________

do while .T.

   qesco(08,15,@cAMBIENTE,sBLOCO1)

   if lastkey() == K_ESC ; exit ; endif

   qsay(08,15,qabrev(cAMBIENTE,"M1",{"Mensal","13. Sal rio"}))

   if lastkey() == K_ESC ; exit ; endif

   qgetx(08,39,@cMESANO,"@R 99/9999")

   if lastkey() == K_ESC ; exit ; endif

   cANOMES  := right(cMESANO,4) + left(cMESANO,2)

   cLOCALIMP := ""

   // VAI FAZER SELECAO DE EMPRESAS ? _______________________________________

   private aEMP := qmarcaemp()

   if lastkey() == K_ESC ; exit ; endif

   // REALIZA PAUSA P/ SOLICITAR DADOS INDIVIDUAIS ? ________________________

   if alert("Quer informar Valor p/ Aut“nomos;e mensagens individualmente ?",{"SIM","NŽO"}) == 2
      lPAUSA := .F.
   endif

   if lastkey() == K_ESC ; loop ; endif

   // ACIONA IMPRESSOES _____________________________________________________

   if empty(aEMP)
      i_emite_guia()
   else
      for nCONT := 1 to len(aEMP)
          qseldirect(aEMP[nCONT])
          i_emite_guia()
      next
   endif

enddo

return

/////////////////////////////////////////////////////////////////////////////
// EMISSAO DAS GUIAS SELECIONADAS ___________________________________________

function i_emite_guia

   local lEMISSAO_OK := .T.

   // ABRE OS ARQUIVOS MANUALMENTE __________________________________________

   if ! quse(XDRV_RH,"FUN"  ,{"FU_GRPS" },"R") ; return .F. ; endif
   if ! quse(XDRV_RH,"SITUA",{"SI_MATRI"},"R") ; return .F. ; endif
   if ! quse(XDRV_RH,"BASE" ,{"BA_MATRI"},"R") ; return .F. ; endif
   if ! quse(XDRV_RH,"LANC" ,{"LA_MATRI"},"R") ; return .F. ; endif
   if ! quse(XDRV_RH,"GRPS" ,{"GR_CODIG"},"R") ; return .F. ; endif
   if ! quse(XDRV_RH,"CONFIG") ; return .F. ; endif
   
   SITUA->(dbSetFilter({|| Anomes == cANOMES},"Anomes == cANOMES"))

   GRPS->(dbSetFilter({|| Anomes == cANOMES},"Anomes == cANOMES"))
   
   // RELACIONAMENTO ________________________________________________________

   FUN->(dbSetRelation( "BASE" , {|| Matricula+cANOMES}, "Matricula+cANOMES" ))

   GRPS->(dbSetRelation( "FUN" , {|| Codigo}, "Codigo" ))

   GRPS->(dbgotop())

   // LOOP DE GRPS E SOLICITACAO DE DADOS ___________________________________

   do while ! GRPS->(eof())

      qrsay(1,XRAZAO)
      qrsay(2,GRPS->Descricao)

      XNIVEL  := 3
      nAUTO   := 0
      cMENSA1 := GRPS->Mensa_1
      cMENSA2 := GRPS->Mensa_2

      if lPAUSA
         qgetx(-1,0,@nAUTO,"@E 999,999.99")
         if lastkey() == K_ESC
            lEMISSAO_OK := .F.
            keyboard chr(13)+chr(13)
         endif
         XNIVEL++
         qgetx(-1,0,@cMENSA1)
         if lastkey() == K_ESC
            lEMISSAO_OK := .F.
            keyboard chr(13)
         endif
         XNIVEL++
         qgetx(-1,0,@cMENSA2)
         if lastkey() == K_ESC
            lEMISSAO_OK := .F.
         endif
      endif

      if ! lEMISSAO_OK ; exit ; endif

      i_emissao()   // emissao de guia individual...

      GRPS->(dbskip())

   enddo

   // FECHA OS ARQUIVOS MANUALMENTE E RETORNA _______________________________

   FUN->(dbclosearea())
   SITUA->(dbclosearea())
   BASE->(dbclosearea())
   LANC->(dbclosearea())
   GRPS->(dbclosearea())
   CONFIG->(dbclosearea())

return

/////////////////////////////////////////////////////////////////////////////
// EMISSAO DO GRPS __________________________________________________________

static function i_emissao

   local nTOTSEG := nTOTEMP := nTOTTER := nTOTDED := nTOTLIQ := 0

   local nSOMA, nQUANTFUN := nTBASE1 := nTBASE2 := 0

   local cCOMPET, cPIC := "@E 9,999,999.99"

   cCOMPET := subs(cANOMES,5,1) + "    " + subs(cANOMES,6,1) + "    "
   cCOMPET += subs(cANOMES,3,1)   + "    " + subs(cANOMES,4,1)

   // INICIALIZA PROCESSO DE IMPRESSAO ______________________________________

   if ! qinitprn(0,cLOCALIMP) ; return ; endif
   qmensa("imprimindo...")

   if empty(cLOCALIMP)
      cLOCALIMP := XLOCALIMP
   endif

   // LOOP NOS FUNCIONARIOS PARA TOTALIZACOES _______________________________

   do while ! FUN->(eof()) .and. FUN->Grps == GRPS->Codigo
      SITUA->(dbseek(FUN->Matricula))

      qgirabarra()

      qmensa("Aguarde...Verificando dados para impress„o...")

      // SALTA FUNCIONARIOS HOMOLOGADOS OU ADMITIDOS POSTERIORMENTE _________

      if SITUA->Situacao $ " H" .or. FUN->Data_Adm > qfimmes(XDATASYS)
         FUN->(dbskip())
         loop
      endif

      // SALTA FUNCIONARIOS TRANSFERIDOS PARA OUTRA EMPRESA _________________

      if SITUA->Af_cod == "40"
         FUN->(dbskip())
         loop
      endif

      // SOMA QUANTIDADE DE FUNCIONARIOS ATIVOS _____________________________

      if SITUA->Vinculo <> K_DIRETOR
         nQUANTFUN++
      endif

      // TOTALIZA BASE ______________________________________________________

      if LANC->(dbseek(cANOMES+FUN->Matricula+"MS105"))
         if cAMBIENTE == "M"
            nTBASE2 += LANC->Valor
         else
            nTBASE2 := 0.00
         endif
      else
         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS106"))
            if cAMBIENTE == "M"
               nTBASE2 += LANC->Valor
            else
               nTBASE2 := 0.00
            endif
         else
            if cAMBIENTE == "M"
               nTBASE1 += BASE->T_inssms + BASE->T_inssfr + BASE->T_inssdt
            else
               nTBASE1 += BASE->T_inss13
            endif
         endif
      endif

      // TOTALIZA VALOR DESCONTADO EM FOLHA _________________________________

      if cAMBIENTE == "M"
         nTOTSEG += BASE->V_inssms + BASE->V_inssfr + BASE->V_inssdt
      else
         nTOTSEG += BASE->V_inss13
      endif

      // TOTALIZA VALOR A DESCONTAR NA GUIA _________________________________
      
      if cAMBIENTE == "M"
         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS901")) // SAL.FAM. DO MES
            nTOTDED += LANC->Valor
         endif

         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS902")) // SAL.FAM. MESES ANT.
            nTOTDED += LANC->Valor
         endif

//         nVALOR:= i_inss(FUN->Matricula)
         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS161")) // SAL. MATERNIDADE MENSAL
            nTOTDED += LANC->Valor //- nVALOR
         endif

//         nVALOR:= i_inss(FUN->Matricula)
         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS281")) // SAL. MATERNIDADE HORA/AULA CURSINHO
            nTOTDED += LANC->Valor //- nVALOR
         endif

//         nVALOR:= i_inss(FUN->Matricula)
         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS282")) // SAL. MATERNIDADE HORA/AULA R1
            nTOTDED += LANC->Valor //- nVALOR
         endif

//         nVALOR:= i_inss(FUN->Matricula)
         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS283")) // SAL. MATERNIDADE HORA/AULA R2
            nTOTDED += LANC->Valor //- nVALOR
         endif

//         nVALOR:= i_inss(FUN->Matricula)
         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS284")) // SAL. MATERNIDADE HORA/AULA R2-1
            nTOTDED += LANC->Valor //- nVALOR
         endif

//         nVALOR:= i_inss(FUN->Matricula)
         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS285")) // SAL. MATERNIDADE HORA/AULA R2-2
            nTOTDED += LANC->Valor //- nVALOR
         endif

//         nVALOR:= i_inss(FUN->Matricula)
         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS286")) // SAL. MATERNIDADE HORA/AULA R3
            nTOTDED += LANC->Valor //- nVALOR
         endif

//         nVALOR:= i_inss(FUN->Matricula)
         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS287")) // SAL. MATERNIDADE HORA/AULA S1
            nTOTDED += LANC->Valor //- nVALOR
         endif

//         nVALOR:= i_inss(FUN->Matricula)
         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS288")) // SAL. MATERNIDADE HORA/AULA S2
            nTOTDED += LANC->Valor //- nVALOR
         endif

//         nVALOR:= i_inss(FUN->Matricula)
         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS289")) // SAL. MATERNIDADE HORA/AULA TST
            nTOTDED += LANC->Valor //- nVALOR
         endif

         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS290")) // DSR SOBRE SALARIO MATERNIDADE HORA/AULA
            nTOTDED += LANC->Valor
         endif

         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS291")) // PRODUTIVIDADE SOBRE SALARIO MATERNIDADE HORA/AULA
            nTOTDED += LANC->Valor
         endif

         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS292")) // HORA ATIVIDADE SOBRE SALARIO MATERNIDADE HORA/AULA
            nTOTDED += LANC->Valor
         endif

         if LANC->(dbseek(cANOMES+FUN->Matricula+"MS480")) // AUXILIO NATALIDADE
            nTOTDED += LANC->Valor
         endif
      endif

      FUN->(dbskip())

   enddo

   qmensa()

   // TOTALIZA AUTONOMOS, EMPRESA E TERCEIROS _______________________________

   nTOTEMP := nTBASE1 * (GRPS->Perc_empr + GRPS->Perc_acid) / 100
   nTOTEMP += nTBASE2 * GRPS->Perc_prol / 100
   nTOTEMP += nAUTO   * GRPS->Perc_auto / 100
   nTBASE2 += nAUTO
   nTOTTER := nTBASE1 * GRPS->Perc_terc / 100

   // APLICA ROUND PARA EVITAR PROBLEMAS DE ERRO NA SOMA ____________________

   nTOTSEG := round(nTOTSEG,2)
   nTOTEMP := round(nTOTEMP,2)
   nTOTTER := round(nTOTTER,2)
   nTOTDED := round(nTOTDED,2)

   // IMPRESSAO DA GUIA _____________________________________________________

   if ! qlineprn() ; return ; endif

   @ prow()+1,000 say XCOND1

   @ prow()+1,075 say iif(!empty(GRPS->Cgc),"1","2")
   @ prow()  ,098 say iif(!empty(GRPS->Cgc),GRPS->Cgc,GRPS->Cei)

   if empty(GRPS->Cgc)
      @ prow()+1,025 say ""
   else
      @ prow()+1,025 say XAEXPAN + GRPS->Cgc + XDEXPAN
   endif

      @ prow()+1,075 say GRPS->Cod_fpas

//   if cAMBIENTE == "M"
//      @ prow()+1,075 say GRPS->Cod_fpas
//   else
//      @ prow()+1,075 say "752"
//   endif

   if empty(GRPS->Cgc)
      @ prow()+1,025 say ""
      @ prow()+1,025 say ""
   else
      @ prow()+1,025 say left(XRAZAO,42) + iif(!empty(subs(XRAZAO,43)),"-","")
      @ prow()+1,025 say subs(XRAZAO,43)
   endif

   if cAMBIENTE == "M"
      @ prow(),075 say cCOMPET
   else
      @ prow(),075 say "1    3    " + subs(cANOMES,3,1)   + "    " + subs(cANOMES,4,1)
   endif

   if empty(GRPS->Cgc)
      @ prow()+1,025 say ""
      @ prow()+1,025 say ""
   else
      @ prow()+1,025 say GRPS->Endereco
      @ prow()+1,025 say transform(GRPS->Cep,"@R 99.999-999")
   endif

   CGM->(dbseek(GRPS->Cgm))

   if empty(GRPS->Cgc)
      @ prow()+1,025 say ""
   else
      @ prow()+1,025 say alltrim(CGM->Municipio) + " - " + CGM->Estado
   endif

   @ prow()  ,115 say nTOTSEG picture cPIC

   if CONFIG->Gr_simples != "S"
      @ prow()+2,115 say nTOTEMP picture cPIC
   else
      @ prow()+2,0 say space(1)
   endif

   @ prow()+1,005 say XRAZAO

   if CONFIG->Gr_simples != "S"
      @ prow()+1,098 say GRPS->Cod_terc
      @ prow()  ,115 say nTOTTER picture cPIC
   else
      @ prow()+1,0 say space(1)
   endif

   @ prow()+3,005 say GRPS->Endereco
   @ prow()+1,005 say GRPS->Bairro
   @ prow()+1,055 say GRPS->Telefone

   @ prow()+1,115 say nTOTDED picture cPIC

   @ prow()+1,005 say transform(GRPS->Cep,"@R 99.999-999") + "     " + CGM->Municipio + space(14) + CGM->Estado

   if CONFIG->Gr_simples == "S"
      nSOMA := nTOTSEG - nTOTDED
   else
      nSOMA := nTOTSEG + nTOTEMP + nTOTTER - nTOTDED
   endif

   @ prow()+1,115 say nSOMA picture cPIC

   @ prow()+1,057 say nQUANTFUN picture "999"

   @ prow()+2,040 say nTBASE1 picture cPIC

   if CONFIG->Gr_simples != "S"
      @ prow()+2,040 say nTBASE2 picture cPIC
   else
      @ prow()+2,0 say space(1)
   endif

   @ prow()+2,040 say GRPS->Cod_sat

   @ prow()+2,005 say cMENSA1
   @ prow()+1,005 say cMENSA2

   @ prow()+5,000 say ""

   qstopprn(.F.)

return
